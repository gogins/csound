<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
    <script src="csound.js"></script>
 </head>
<body>
    <script>
    var handleMessage = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + '\n' + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var moduleDidLoad = function() {
        document.getElementById('console').value += 'moduleDidLoad was called; Csound functions may now be called.';
        console.warn = handleMessage;
    }
    var onPlayClick = function() {
        var csd = document.getElementById('csd').value;
        csound.compileCsdText(csd);
        csound.start();
        csound.perform();
    }
    var onPlayStop = function() {
        csound.stop();
    }
  </script>
<h1>linlin</h1>
<p>
This example will play if your Web browser is a desktop version of Google Chrome with WebAssembly enabled. You can edit and replay the code. At this time, most but not all examples will run in WebAssembly.
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<p>
<textarea id="csd" style="width: 100%; height: 50%;font-size:12px;"><CsoundSynthesizer>
<CsOptions>
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 128
nchnls = 2 ; Changed for WebAssembly output from: = 2
0dbfs = 1.0

; Example file for linlin.csd

/*

linlin

linear to linear interpolation between two ranges

ky    linlin kx, ky0, ky1, kx0=0, kx1=1
kys[] linlin kxs[], ky0, ky1, kx0=0, kx1=1
iys[] linlin ixs[], iy0, iy1, ix0=0, ix1=1

linlin can be also used to blend between two arrays:

kC[]  linlin kx, kA[], kB[], kx0=0, kx1=1

if kx==0.5, kC[] will hold the avg of kA[] and kB[] for each value
(assumes that kA[] and kB[] are arrays of the same size)

*/

; Map a value within the range 1-3 to the range 0-10.
instr 1
  kx line 1, p3, 3
  ky linlin kx, 0, 10, 1, 3
  printks "kx: %f   ky: %f \n", 1/kr, kx, ky
endin

; Map an array of values
instr 2
  kX[] fillarray 0, 0.5, 1, 1.5, 2
  kY[] linlin kX, 0, 10, 0, 2
  printarray kY, 1, "", "instr 2, kY"
  turnoff
endin

; Blend between two arrays
instr 3
  kA[] fillarray 0, 1, 2, 3, 4, 5
  kB[] fillarray 0, 2, 4, 6, 8, 10
  kx line 0, p3, 1
  kC[] linlin kx, kA, kB
  printarray kC, -1, "", "blend"
endin

</CsInstruments>
<CsScore>
i 1 0   0.2
i 2 0.5 0.2
i 3 1 0.5

</CsScore>
</CsoundSynthesizer>
</textarea>
<h3>Csound Messages</h3>
<textarea id="console" readonly style="width: 100%; height: 25%;font-size:12px;">
</textarea>
</body>
</html>