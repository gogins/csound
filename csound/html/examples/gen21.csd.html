<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<title>adsr</title>
<link rel="stylesheet" type="text/css" href="csound.css" />
<meta name="generator" content="DocBook XSL Stylesheets V1.78.1" />
<link rel="home" href="index.html" title="The Canonical Csound Reference Manual" />
<link rel="up" href="OpcodesTop.html" title="Orchestra Opcodes and Operators" />
<link rel="prev" href="active.html" title="active" />
<link rel="next" href="adsyn.html" title="adsyn" />
</head>
<h1>examples/gen21.csd.html</h1>
<p>
This example will play if your Web browser is a desktop version of Google Chrome with PNaCl enabled. You can edit and replay the code. At this time, most but not all examples will run in PNaCl.
</p>
<p>
<input type="button" value="Play" onclick="start_onclick()"/>
<input type="button" value="Stop" onclick="stop_onclick()"/>
<p>
<textarea id="csd" style="width: 100%; height: 50%;font-size:12px;"><CsoundSynthesizer>
<CsOptions>
; Select audio/midi flags here according to platform
-odac  ;;;realtime audio out
;-iadc    ;;;uncomment -iadc if realtime audio input is needed too
; For Non-realtime ouput leave only the line below:
; -o gen21.wav -W ;;; for file output any platform
</CsOptions>
<CsInstruments>

sr = 44100
ksmps = 32
nchnls = 2
0dbfs  = 1

instr 1

ifn    = p4
isize  = ftlen(ifn) 
prints "TABLE NUMBER: %d\n", ifn   
prints "Index\tValue\n"
    
iindex = 0				;start loop
begin_loop:
     ivalue tab_i iindex, ifn
     prints "%d:\t%f\n", iindex, ivalue
     iindex = iindex + 1
  if (iindex < isize) igoto begin_loop
  
;produce sound - and repeat it 10 times so you can hear the patterns:   
aphase phasor 10/10			;play all 32 values 10x over 10 seconds
aphase = aphase*isize			;step through table
afrq   table aphase, p4			;read table number
asig   poscil .5, (afrq*500)+1000,10	;scale values of table 500 times, add 1000 Hz
       outs asig , asig			;so we can distinguish the different tables 
endin

</CsInstruments>
<CsScore>
f1 0 32 21 1		;Uniform (white noise)
f2 0 32 21 6		;Gaussian (mu=0.0, sigma=1/3.83=0.261)
f3 0 32 21 6 5.745	;Gaussian (mu=0.0, sigma=5.745/3.83=1.5)
f4 0 32 21 9 1 1 2	;Beta (note that level precedes arguments)
f5 0 32 21 10 1 2	;Weibull
f10 0 8192 10 1		;Sine wave

i 1  0 10 1
i 1 11 10 2
i 1 22 10 3
i 1 33 10 4
i 1 44 10 5
e
</CsScore>
</CsoundSynthesizer>

</textarea>
<h3>Csound Messages</h3>
<textarea id="console" readonly style="width: 100%; height: 25%;font-size:12px;">
</textarea>
<script type="text/javascript" src="../../csound.js"></script>
<script>
function handleMessage(message) {
    console.log(message);
    var messages_textarea = document.getElementById("console");
    var existing = messages_textarea.value;
    messages_textarea.value = existing + message.data;
    messages_textarea.scrollTop = messages_textarea.scrollHeight;    
}
function sleep (time) {
    return new Promise((resolve) => setTimeout(resolve, time));
}
function start_onclick() {
    csound.stop();
    sleep(500).then(() => {
        var csd = document.getElementById('csd').value;
        csound.compileCsdText(csd);
    })    
}
function stop_onclick () {
    csound.stop();
}
</script>
 <div id="engine"></div>
</body>
</html>