
<!DOCTYPE html>
<html>
<head>
    <title>Csound HTML Tutorial 02</title>
    <meta http-equiv="Pragma" content="no-cache">
    <meta http-equiv="Expires" content="-1"></head>
<body>
    <h1>Csound HTML Tutorial 02</h1>
    <h2>Introduction</h3>
    <p>The purpose of this tutorial is to show you how to enable users to interactively control the instruments playing in 
    a Csound piece that has been embedded into a Web page. You can play the piece, stop it from playing, use the sliders 
    to control the sounds of the instruments as they play, view and edit the Csound code, and see Csound's diagnostic 
    messages. You may find this piece immediately below. Go ahead and try it -- it will play on version 31 or later of 
    Google's Chrome browser for OS X, Windows, or Linux. A thorough explanation of the steps required to create this 
    Web page using the PNaCl build of Csound follows.</p>
    <h2>Dunes v 2</h2>
    <h4>By Michael Gogins</h3>
    <h4>2015</h4> 
    
    <form id='persist'>
    
    <table>
    <col width="2*">
    <col width="5*">
    <col width="100px">

    <th colspan=3 align='left'>Droner</th>
    <tr>
    <td>
    <label for=gk_Droner_slider1>gk_Droner_slider1</label>
    <td>
    <input class=persistent-element type=range min=0 max=1 value=.25 id=gk_Droner_slider1 step=.001>
    <td>
    <output for=gk_Droner_slider1 id=gk_Droner_slider1_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Droner_slider2>gk_Droner_slider2</label>
    <td>
    <input class=persistent-element type=range min=0 max=1 value=.5 id=gk_Droner_slider2 step=.001>
    <td>
    <output for=gk_Droner_slider2 id=gk_Droner_slider2_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Droner_slider3>gk_Droner_slider3</label>
    <td>
    <input class=persistent-element type=range min=0 max=1 value=.75 id=gk_Droner_slider3 step=.001>
    <td>
    <output for=gk_Droner_slider3 id=gk_Droner_slider3_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Droner_slider4>gk_Droner_slider4</label>
    <td>
    <input class=persistent-element type=range min=0 max=1 value=.5 id=gk_Droner_slider4 step=.001>
    <td>
    <output for=gk_Droner_slider4 id=gk_Droner_slider4_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Droner_slider5>gk_Droner_slider5</label>
    <td>
    <input class=persistent-element type=range min=0 max=1 value=.125 id=gk_Droner_slider5 step=.001>
    <td>
    <output for=gk_Droner_slider5 id=gk_Droner_slider5_output>.5</output>
    </tr>

    <th colspan=3 align='left'>Buzzer</th>
    <tr>
    <td>
    <label for=gk_Buzzer_harmonics>gk_Buzzer_harmonics</label>
    <td>
    <input class=persistent-element type=range min=0 max=20 value=3 id=gk_Buzzer_harmonics step=.001>
    <td>
    <output for=gk_Buzzer_harmonics id=gk_Buzzer_harmonics_output>.5</output>
    </tr>

    <th colspan=3 align='left'>Sweeper</th>
    <tr>
    <td>
    <label for=gk_Sweeper_britel>gk_Sweeper_britel</label>
    <td>
    <input class=persistent-element type=range min=0 max=5 value=.25 id=gk_Sweeper_britel step=.001>
    <td>
    <output for=gk_Sweeper_britel id=gk_Sweeper_britel_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Sweeper_briteh>gk_Sweeper_briteh</label>
    <td>
    <input class=persistent-element type=range min=0 max=5 value=.25 id=gk_Sweeper_briteh step=.001>
    <td>
    <output for=gk_Sweeper_briteh id=gk_Sweeper_briteh_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Sweeper_britels>gk_Sweeper_britels</label>
    <td>
    <input class=persistent-element type=range min=0 max=5 value=.25 id=gk_Sweeper_britels step=.001>
    <td>
    <output for=gk_Sweeper_britels id=gk_Sweeper_britels_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Sweeper_britehs>gk_Sweeper_britel</label>
    <td>
    <input class=persistent-element type=range min=0 max=5 value=.25 id=gk_Sweeper_britehs step=.001>
    <td>
    <output for=gk_Sweeper_britehs id=gk_Sweeper_britehs_output>.5</output>
    </tr>

    <th colspan=3 align='left'>Blower</th>
    <tr>
    <td>
    <label for=gk_Blower_grainDensity>gk_Blower_grainDensity</label>
    <td>
    <input class=persistent-element type=range min=0 max=500 value=150 id=gk_Blower_grainDensity step=.001>
    <td>
    <output for=gk_Blower_grainDensity id=gk_Blower_grainDensity_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Blower_grainDuration>gk_Blower_grainDuration</label>
    <td>
    <input class=persistent-element type=range min=0 max=2 value=.25 id=gk_Blower_grainDuration step=.001>
    <td>
    <output for=gk_Blower_grainDuration id=gk_Blower_grainDuration_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Blower_grainAmplitudeRange>gk_Blower_grainAmplitudeRange</label>
    <td>
    <input class=persistent-element type=range min=0 max=500 value=100 id=gk_Blower_grainAmplitudeRange step=.001>
    <td>
    <output for=gk_Blower_grainAmplitudeRange id=gk_Blower_grainAmplitudeRange_output>100</output>
    </tr>
    <tr>
    <td>
    <label for=gk_Blower_grainFrequencyRange>gk_Blower_grainFrequencyRange</label>
    <td>
    <input class=persistent-element type=range min=0 max=2 value=.033 id=gk_Blower_grainFrequencyRange step=.001>
    <td>
    <output for=gk_Blower_grainFrequencyRange id=gk_Blower_grainFrequencyRange_output>.5</output>
    </tr>

    <th colspan=3 align='left'>Master</th>
    <tr>
    <td>
    <label for=gk_Reverb_Feedback>gk_Reverb_Feedback</label>
    <td>
    <input class=persistent-element type=range min=0 max=1 id=gk_Reverb_Feedback step=.001>
    <td>
    <output for=gk_Reverb_Feedback id=gk_Reverb_Feedback_output>.5</output>
    </tr>
    <tr>
    <td>
    <label for=gk_MasterOutput_gain>gk_MasterOutput_gain</label>
    <td>
    <input class=persistent-element type=range min=0 max=5 id=gk_MasterOutput_gain step=.001>
    <td>
    <output for=gk_MasterOutput_gain id=gk_MasterOutput_gain_output>.5</output>
    </tr>

    </table>
 
    <br>
    <input type="button" id='play' value="Play" />
    <input type="button" id='stop' value="Stop" />
    <input type="button" id='save' value="Save" />
    <input type="button" id='restore' value="Restore" />

    </form>
    <h4>Csound Errors, Warnings, and Messages</h4>
    <textarea id="console" rows=10 cols=80></textarea>
    <h4>Csound Orchestra</h4>
    <textarea id="orc" rows=24 cols=80>
opcode hertz2midinn, i, i
ihertz xin
print ihertz
; m = 12*log2(fm/440 Hz) + 69
ilog2 = log(2)
imidinn = 12 * (log(ihertz / 440) / ilog2) + 69
print imidinn
xout imidinn
endop

instr 101
insno = p1
istart = p2
iduration = p3
ihertz = p4
ivelocity = p5
ipan = p6
ikey hertz2midinn ihertz
event_i "i", "Droner", 0, iduration, ikey, ivelocity, 0, ipan
endin

instr 102
insno = p1
istart = p2
iduration = p3
ihertz = p4
ivelocity = p5
ipan = p6
ikey hertz2midinn ihertz
event_i "i", "Buzzer", 0, iduration, ikey, ivelocity, 0, ipan
endin

instr 103
insno = p1
istart = p2
iduration = p3
ihertz = p4
ivelocity = p5
ipan = p6
ikey hertz2midinn ihertz
event_i "i", "Sweeper", 0, iduration, ikey, ivelocity, 0, ipan
endin

instr 104
insno = p1
istart = p2
iduration = p3
ihertz = p4
ivelocity = p5
ipan = p6
ikey hertz2midinn ihertz
event_i "i", "Blower", 0, iduration, ikey, ivelocity, 0, ipan
endin

gi_Droner_overlap init 20
gk_Droner_distort_factor init 0.4
gk_Droner_first_harmonic init 0.4
gk_Droner_slider1 init .05
gk_Droner_slider2 init .1
gk_Droner_slider3 init .04
gk_Droner_slider4 init .2
gk_Droner_slider5 init .02
gi_Droner_sine ftgen 0, 0, 65536, 10, 1, 0, .02
instr Droner
insno = p1
istart = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p6
ipan = p7
k1 init .5
k2 init .05
k3 init .1
k4 init .2
k5 init .1
k6 init .05
k7 init .1
k8 init 0
k9 init 0
k10 init 0
k3 = gk_Droner_slider1
k4 = gk_Droner_slider2
k5 = gk_Droner_slider3
k6 = gk_Droner_slider4
k7 = gk_Droner_slider5
kwaveform init 0
iamp = ampdb(ivelocity)
iattack = gi_Droner_overlap
idecay = gi_Droner_overlap
isustain = p3 - gi_Droner_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
if kwaveform == 0 then
asignal poscil3 1, ihertz, gi_Droner_sine
endif
if kwaveform == 1 then
asignal vco2 1, ihertz, 8 ; integrated saw
endif
if kwaveform == 2 then
asignal vco2 1, ihertz, 12 ; triangle
endif
asignal chebyshevpoly asignal, 0, k1, k2, k3, k4, k5, k6, k7, k8, k9, k10
asignal = asignal * kenvelope * 10
aleft, aright pan2 asignal, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
aleft = adamping * aleft
aright = adamping * aright
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Bower_overlap init 20
gi_Bower_sine ftgen 0,0,65536,10,1
instr Bower
insno = p1
istart = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p6
ipan = p7
iamp = ampdb(ivelocity) * 200
iattack = gi_Bower_overlap
idecay = gi_Bower_overlap
isustain = p3 - gi_Bower_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
kamp = kenvelope
kfreq = ihertz
kpres = 0.25
; krat rspline 0.006,0.988,0.1,0.4
krat rspline 0.006,0.988,1,4
kvibf = 4.5
kvibamp = 0
iminfreq = 20
aSig wgbow kamp,kfreq,kpres,krat,kvibf,kvibamp,gi_Bower_sine,iminfreq
;aSig butlp aSig,2000
;aSig pareq aSig,80,6,0.707
aleft, aright pan2 aSig / 7, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Phaser_overlap init 20
gk_Phaser_ratio1 init 1
gk_Phaser_ratio2 init 1/3
gk_Phaser_index1 init 1
gk_Phaser_index2 init 0.0125
gi_Phaser_sine ftgen 0,0,65536,10,1
instr Phaser
insno = p1
istart = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p6
ipan = p7
iamp = ampdb(ivelocity) * 8
iattack = gi_Phaser_overlap
idecay = gi_Phaser_overlap
isustain = p3 - gi_Phaser_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
khertz = ihertz
ifunction1 = gi_Phaser_sine
ifunction2 = gi_Phaser_sine
a1,a2 crosspm gk_Phaser_ratio1, gk_Phaser_ratio2, gk_Phaser_index1, gk_Phaser_index2, khertz, ifunction1, ifunction2
aleft, aright pan2 a1+a2, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
aleft = adamping * aleft * kenvelope
aright = adamping * aright * kenvelope
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Sweeper_overlap init 20
gk_Sweeper_britel init 0
gk_Sweeper_briteh init 2.9
gk_Sweeper_britels init .2 / 3
gk_Sweeper_britehs init 2.5 / 2
gi_Sweeper_sine ftgen 0, 0, 65536, 10, 1
gi_Sweeper_octfn ftgen 0, 0, 65536, -19, 1, 0.5, 270, 0.5
instr Sweeper
insno = p1
istart = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p6
ipan = p7
iamp = ampdb(ivelocity)
iattack = gi_Sweeper_overlap
idecay = gi_Sweeper_overlap
isustain = p3 - gi_Sweeper_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
icps = ihertz
kamp expseg 0.001,0.02,0.2,p3-0.01,0.001
ktonemoddep jspline 0.01,0.05,0.2
ktonemodrte jspline 6,0.1,0.2
ktone poscil3 ktonemoddep, ktonemodrte, gi_Sweeper_sine
kbrite rspline gk_Sweeper_britel, gk_Sweeper_briteh, gk_Sweeper_britels, gk_Sweeper_britehs
ibasfreq init icps
ioctcnt init 3
iphs init 0
;a1 hsboscil kamp, ktone, kbrite, ibasfreq, gi_Sweeper_sine, gioctfn, ioctcnt, iphs
a1 hsboscil kenvelope, ktone, kbrite, ibasfreq, gi_Sweeper_sine, gi_Sweeper_octfn, ioctcnt, iphs
amod poscil3 0.25, ibasfreq*(1/3), gi_Sweeper_sine
arm = a1*amod
kmix expseg 0.001, 0.01, rnd(1), rnd(3)+0.3, 0.001
kmix=.25
a1 ntrpol a1, arm, kmix
;a1 pareq a1/10, 400, 15, .707
;a1 tone a1, 500
kpanrte jspline 5, 0.05, 0.1
kpandep jspline 0.9, 0.2, 0.4
kpan poscil3 kpandep, kpanrte, gi_Sweeper_sine
a1,a2 pan2 a1, kpan
a1 delay a1, rnd(0.1)
a2 delay a2, rnd(0.11)
kenv linsegr 1, 1, 0
kenv = kenvelope
aleft = a1*kenv*.02
aright = a2*kenv*.02
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
aleft = adamping * aleft
aright = adamping * aright
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Buzzer_overlap init 20
gk_Buzzer_harmonics init 2
gk_Buzzer_distort_factor init 0.4
gk_Buzzer_first_harmonic init 0.4
gi_Buzzer_sine ftgen 0, 0, 65536, 10, 1
instr Buzzer
insno = p1
istart = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p6
ipan = p7
iamp = ampdb(ivelocity) * 4
iattack = gi_Buzzer_overlap
idecay = gi_Buzzer_overlap
isustain = p3 - gi_Buzzer_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
asignal buzz kenvelope, ihertz, gk_Buzzer_harmonics, gi_Buzzer_sine
asignal = asignal * 3
aleft, aright pan2 asignal, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
aleft = adamping * aleft
aright = adamping * aright
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Shiner_overlap init 20
gk_Shiner_harmonics init 5
gk_Shiner_harmonics init 2
gk_Shiner_distort_factor init 0.4
gk_Shiner_first_harmonic init 0.4
gi_Shiner_sine ftgen 0, 0, 65536, 10, 1
instr Shiner
insno = p1
istart = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p6
ipan = p7
iamp = ampdb(ivelocity) * 4
iattack = gi_Shiner_overlap
idecay = gi_Shiner_overlap
isustain = p3 - gi_Shiner_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
asignal vco2 kenvelope * 4, ihertz, 12
aleft, aright pan2 asignal, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
aleft = adamping * aleft
aright = adamping * aright
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Blower_overlap init 20
gk_Blower_grainDensity init 150
gk_Blower_grainDuration init 0.2
gk_Blower_grainAmplitudeRange init 100
gk_Blower_grainFrequencyRange init .033
gi_Blower_grtab ftgen 0, 0, 65536, 10, 1, .3, .1, 0, .2, .02, 0, .1, .04
gi_Blower_wintab ftgen 0, 0, 65536, 10, 1, 0, .5, 0, .33, 0, .25, 0, .2, 0, .167
instr Blower
i_instrument = p1
i_time = p2
i_duration = p3
i_midikey = p4
i_midivelocity = p5
i_phase = p6
i_pan = p6
i_depth = p8
i_height = p9
i_pitchclassset = p10
i_homogeneity = p11
ifrequency = cpsmidinn(i_midikey)
iamplitude = ampdb(i_midivelocity) / 200
 ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
 ; f1 0 65536 1 "hahaha.aif" 0 4 0
 ; f2 0 1024 7 0 224 1 800 0
 ; f3 0 8192 7 1 8192 -1
 ; f4 0 1024 7 0 512 1 512 0
 ; f5 0 1024 10 1 .3 .1 0 .2 .02 0 .1 .04
 ; f6 0 1024 10 1 0 .5 0 .33 0 .25 0 .2 0 .167
 ; a0 14 50
 ; p1 p2 p3 p4 p5 p6 p7 p8 p9 p10
 ; Start Dur Amp Freq GrTab WinTab FqcRng Dens Fade
 ; i1 0.0 6.5 700 9.00 5 4 .210 200 1.8
 ; i1 3.2 3.5 800 7.08 . 4 .042 100 0.8
 ; i1 5.1 5.2 600 7.10 . 4 .0320 100 0.9
 ; i1 7.2 6.6 900 8.03 . 4 .021 150 1.6
 ; i1 21.3 4.5 1000 9.00 . 4 .031 150 1.2
 ; i1 26.5 13.5 1100 6.09 . 4 .121 150 1.5
 ; i1 30.7 9.3 900 8.05 . 4 .014 150 2.5
 ; i1 34.2 8.8 700 10.02 . 4 .14 150 1.6
iHz = ifrequency
ihertz = iHz
ip4 = iamplitude
ip5 = iHz
ip6 = gi_Blower_grtab
ip7 = gi_Blower_wintab
ip8 = 0.033
ip8 = .002
ip9 = 150
ip9 = 100
ip10 = 1.6
ip10 = 3
idur = p3
iamp = iamplitude ; p4
ifqc = iHz ; cpspch(p5)
igrtab = ip6
iwintab = ip7
ifrng = ip8
idens = ip9
ifade = ip10
igdur = 0.2
iattack = gi_Blower_overlap
idecay = gi_Blower_overlap
isustain = p3 - gi_Blower_overlap
p3 = iattack + isustain + idecay
kenvelope transeg 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
; kamp linseg 0, ifade, 1, idur - 2 * ifade, 1, ifade, 0
kamp = kenvelope
; Amp Fqc Dense AmpOff PitchOff GrDur GrTable WinTable MaxGrDur
aoutl grain ip4, ifqc, gk_Blower_grainDensity, gk_Blower_grainAmplitudeRange, ifqc * gk_Blower_grainFrequencyRange, gk_Blower_grainDuration, gi_Blower_grtab, gi_Blower_wintab, 5
aoutr grain ip4, ifqc, gk_Blower_grainDensity, gk_Blower_grainAmplitudeRange, ifqc * gk_Blower_grainFrequencyRange, gk_Blower_grainDuration, gi_Blower_grtab, gi_Blower_wintab, 5
aleft = aoutl * kamp * iamplitude
aright = aoutr * kamp * iamplitude
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
aleft = adamping * aleft
aright = adamping * aright
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

instr Controls
gk_Droner_slider1 chnget "gk_Droner_slider1"
gk_Droner_slider2 chnget "gk_Droner_slider2"
gk_Droner_slider3 chnget "gk_Droner_slider3"
gk_Droner_slider4 chnget "gk_Droner_slider4"
gk_Droner_slider5 chnget "gk_Droner_slider5"
gk_Buzzer_harmonics chnget "gk_Buzzer_harmonics"
gk_Blower_grainDensity chnget "gk_Blower_grainDensity"
gk_Blower_grainDuration chnget "gk_Blower_grainDuration"
gk_Blower_grainAmplitudeRange chnget "gk_Blower_grainDuration"
gk_Blower_grainFrequencyRange chnget "gk_Blower_grainFrequencyRange"
gk_Reverb_Feedback chnget "gk_Reverb_Feedback"
gk_MasterOutput_gain chnget "gk_MasterOutput_gain"
prints "instr %4d t %12.4f d %12.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gk_Reverb_Feedback init 0.975
gk_Reverb_DelayModulation init 0.875
instr Reverb
ain_left chnget "out_left"
ain_right chnget "out_right"
azero init 0
aout_left init 0
aout_left, aright reverbsc ain_left, azero, gk_Reverb_Feedback, 15000.0
aleft init 0
azero init 0
aleft, aout_right reverbsc azero, ain_right, gk_Reverb_Feedback, 15000.0
chnmix aout_left, "reverb_out_left"
chnmix aout_right, "reverb_out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gk_MasterOutput_gain init .000003
instr MasterOutput
ain_left chnget "reverb_out_left"
ain_right chnget "reverb_out_right"
aout_left = ain_left * gk_MasterOutput_gain * .000001
aout_right = ain_right * gk_MasterOutput_gain * .000001
outs aout_left, aout_right
prints "instr %4d t %12.4f d %12.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

instr Clear
chnclear "out_left"
chnclear "out_right"
chnclear "reverb_out_left"
chnclear "reverb_out_right"
prints "instr %4d t %12.4f d %12.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

scoreline_i "i \"Controls\" 0 -1"
scoreline_i "i \"Reverb\" 0 -1"
scoreline_i "i \"MasterOutput\" 0 -1"
scoreline_i "i \"Clear\" 0 -1"
    </textarea>
    <h4>Csound Score</h4>
    <textarea id="sco" rows=24 cols=80>
; Change the tempo, if you like.
t 0 27

; p1 p2 p3 p4 p5 p6 p7 p8
; insno onset duration fundamental numerator denominator velocity pan

; C E B
; [numerator/denominator*fundamental*octave]
i 101   0 60 [ 1 /  1 * 60 * 1] 60 [-1 + 3 / 2]
i 101   0 60 [ 5 /  4 * 60 * 2] 60 [-1 + 2 / 2]
i 101   0 30 [28 / 15 * 60 * 3] 60 [-1 + 1 / 2]
; C Ab E B
i 104  30 30 [ 8 /  5 * 60 * 1] 60 [-1 + 2 / 2]
; G F# B
i 102  60 60 [ 3 /  2 * 60 * 1] 60 [-1 + 3 / 2]
i 102  60 60 [45 / 32 * 60 * 2] 60 [-1 + 2 / 2]
i 102  60 30 [28 / 15 * 60 * 3] 60 [-1 + 1 / 2]
; G F B
i 102  90 30 [ 4 / 3 * 60 * 3] 60 [-1 + 2 / 2]
; C E B
i 103 120 60 [ 1 /  1 * 60 * 1] 60 [-1 + 1 / 2]
i 103 120 60 [ 5 /  4 * 60 * 2] 60 [-1 + 3 / 2]
i 103 120 30 [28 / 15 * 60 * 2] 60 [-1 + 2 / 2]
i 103 150 30 [ 1 /  1 * 60 * 5] 60 [-1 + 2 / 2]
    </textarea>
    <h2>How It Is Done</h4>
    <p>This tutorial is a simple example. All unnecessary HTML elements and JavaScript function calls have been 
    omitted. This Web page uses only a subset of the Csound API that has the same funtions, names, and signatures 
    in csound.node, Csound for Android, CsoundQt, and Csound for PNaCl. This piece will not run without modification
    in these other environments, although that is a goal for the Csound developers. But the changes required are 
    simple, and I explain them in an Appendix to this chapter, "Interoperable HTML5 for Csound".</p>
    <p>Comments on this page or in the page source explain what each element is for and why it is 
    written the way it is. You can view the code and comments by right-clicking on this page and selecting 
    "View page source".</p>
    <h3>Embedding Csound</h3>
    <p>PNaCl (Portable Native Client, pronounced "pinnacle") is Google's technology for <i>safely</i> 
    running C and C++ code in all desktop versions of Google's Chrome browser. For more information on PNaCl, 
    go <a href='https://developer.chrome.com/native-client'>here</a>. Very briefly:
    <ul>
        <li>PNaCl applications must be loaded from a Web server. In this case, that is GitHub, the host for 
        <a href='http://gogins.github.io/csound/'>http://gogins.github.io</a>.</li>
        <li>PNaCl applications are compiled twice using Google's toolchain: first by the developer to compile a 
        .pexe file for the browser to load, and then by the browser itself to compile the .pexe (portable executable)
        file into native machine language for the browser's CPU as a .nexe (native executable) file that is cached 
        for re-use. Because the application runs as native machine language, it runs only about 5% or 10% slower 
        than a regular compiled C or C++ program. For audio applications, this is a <i>huge</i> advantage.</li>
        <li>The browser knows how to load the application by reading an &lt;embed&gt; tag that points to the 
        .pexe file and to a .nmf manifest file that tells the browser how to compile the .pexe file.</li>
        <li>The application runs in a security sandbox in a separate process, and communicates with the 
        browser only by means of asynchronous messages sent via Google's Pepper API.</li>
    </ul>
    <p>To embed Csound for PNaCl in a Web page, simply load the "csound.js" script using a &lt;script&gt; element. 
    This contains JavaScript that dynamically creates the &lt;embed&gt; element, and also manages sending and 
    receiving messages from the Csound process. As far as any JavaScript code on the Web page is concerned, 
    Csound is just an already existing JavaScript object named "csound", with 
    its own functions and callbacks, which represent important parts of the Csound API.</p>
    <p>It is good practice to place all &lt;script&gt; elements at the very end of the HTML &lt;body&gt; element, 
    so that the other elements have already been created before any scripts try to access them, and that is what 
    I have done here.</p>
    <p>Finally, the very last element in the body must be named "engine". The "csound.js" script obtains this 
    element from the JavaScript document object for the page, and attaches to it the &lt;embed&gt; element and 
    Csound's callbacks.</p>
    <p>The code for embedding Csound for PNaCl in a Web page looks like this:</p>
    <xmp>
	<script type="text/javascript" src="csound.js"></script>
	<div id="engine"></div>
    </xmp>
    <h3>Embedding the Csound Score and Orchestra</h3>
    <p>There are many ways to get a Csound piece into Csound for PNaCl. But in order to make the Csound orchestra 
    and score editable, they must appear in &lt;textarea&gt; elements on the Web page. These can be smaller than 
    the text they contain, yet have scrollbars for accessing all of the text. These textareas must be named, 
    so that we can use JavaScript to retrieve the orchestra and score and pass them to Csound.</p>
    <p>The code for a textarea containing a Csound orchestra looks like this:</p>
    <xmp>
    <textarea id="orc" rows=24 cols=80>
opcode hertz2midinn, i, i
ihertz xin
print ihertz
; m = 12*log2(fm/440 Hz) + 69
ilog2 = log(2)
imidinn = 12 * (log(ihertz / 440) / ilog2) + 69
print imidinn
xout imidinn
endop

instr 101
insno = p1
istart = p2
iduration = p3
ihertz = p4
ivelocity = p5
ipan = p6
ikey hertz2midinn ihertz
event_i "i", "Droner", 0, iduration, ikey, ivelocity, 0, ipan
endin
(...remainder of orchestra...)
    </textarea>    	
    </xmp>
    <p>Please note, Csound system constants are set by "csound.js" and cannot be set in the orchestra header, 
    so they are omitted here. Because "csound.js" sets 0dbfs to 1, but this piece assumes a 0dbfs level 
    of 32768, we have edited the orchestra drastically reduce the output level.</p>
    <p>The code for a textarea containing a Csound score works the same way.</p>
    <p>Also note, Csound for PNaCl can perform scores using either <code>csound.compileOrc(orc_text)</code> 
    followed by <code>csound.readScore(sco_text)</code>, or by simply calling 
    <code>csound.playCsd(filename)</code>. If <code>readScore</code> is used, the score must not contain any 
    "t", "s", or "e" statements, and all score times must be absolute times in seconds from the beginning of 
    the performance. If <code>playCsd</code> is used, any and all standard score statements may be used.</p>
    <h3>Starting and Stopping Csound</h3>
    <p>It is convenient to provide buttons for starting and stopping Csound, rather than simply having it start 
    playing the moment the page is loaded. The JavaScript code for handling the button clicks must start 
    Csound, send the orchestra to Csound, and send the score, in order to perform the piece.</p>
    <p>The buttons and code for the Play and Stop buttons above look like this:</p>
    <xmp>
    <input type="button" value="Play" onclick="start_onclick()"/>
    <input type="button" value="Stop" onclick="stop_onclick()"/>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.11.3/jquery.min.js">
    </script>
    <script>    
    $(document).ready(function() {
    	$.getScript('csound.js');
        $('#play').on('click', function() {      	
            $('#restore').trigger('click');
            csound.perform();
            csound.compileOrc(document.getElementById('orc').value);
            csound.readScore(document.getElementById('sco').value);
        });
        $('#stop').on('click', function() {      	
            csound.stop();
        });
        $('input').on('input', function(event) {
            var slider_value = parseFloat(event.target.value);
            csound.setControlChannel(event.target.id, slider_value);
            var output_selector = '#' + event.target.id + '_output';
            $(output_selector).val(slider_value);
        });
        $('#save').on('click', function() {
            $('.persistent-element').each(function() {
                localStorage.setItem(this.id, this.value);
            });
        });
        $('#restore').on('click', function() {
            $('.persistent-element').each(function() {
                this.value = localStorage.getItem(this.id);
                csound.setControlChannel(this.id, this.value);
                var output_selector = '#' + this.id + '_output';
                $(output_selector).val(this.value);
            });
        });
	$('#restore').trigger('click');    
    // });
    </script>
    </xmp>
    <h3>Handling Csound's Errors, Warnings, and Messages</h3>
    <p>Csound will send all of its runtime messages to a JavaScript function in the Web page named 
    "handleMessage". This callback is automatically invoked by the "csound.js" script. Anything could be done 
    with these messages, including discarding them. Here, we append them to an initially empty textarea element named 
    "console". The "csound.js" script also sends its messages, prefaced "Csound:", to the "console" element; 
    therefore, this one textarea captures all messages from Csound for PNaCl.</p>
    <p>The code for handling messages from Csound looks like this:</p>
    <xmp>
    // Messages both from the "csound.js" wrapper and from Csound itself are combined in the 
    // "console" text area.
    function handleMessage(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message.data;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;    
    }
    </xmp>
    <h3>Next Steps</h3>
    <p>As we proceed through subsequent tutorials, more and more features of HTML5 will be integrated with 
    Csound, adding features to this piece:
    <ul>
    	<li>Control sliders for instruments.</li>
    	<li>More attractive styling of the user interface.</li>
    	<li>User-controllable score generation in JavaScript.</li>
    	<li>An interactive 3-dimensional visual display.</li>
    </ul>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.11.3/jquery.min.js">
    </script>
    <script>    
    // The usual pattern of creating all jQuery handlers in the document's on ready handler,
    // when the DOM has been constructed, does not work here because of the way Csound is 
    // initialized by the csound.js script, and so is commented out here.
    // $(document).ready(function() {
        $('#play').on('click', function() {      	
            csound.perform();
            $('#restore').trigger('click');
            csound.compileOrc(document.getElementById('orc').value);
            csound.readScore(document.getElementById('sco').value);
        });
        $('#stop').on('click', function() {      	
            csound.stop();
        });
        $('input').on('input', function(event) {
            var slider_value = event.target.value;
            csound.setControlChannel(event.target.id, slider_value);
            var output_selector = '#' + event.target.id + '_output';
            $(output_selector).val(slider_value);
        });
        $('#save').on('click', function() {
            $('.persistent-element').each(function() {
                localStorage.setItem(this.id, this.value);
            });
        });
        $('#restore').on('click', function() {
            $('.persistent-element').each(function() {
                this.value = localStorage.getItem(this.id);
                csound.setControlChannel(this.id, this.value);
                var output_selector = '#' + this.id + '_output';
                $(output_selector).val(this.value);
            });
        });
	// Messages both from the "csound.js" wrapper and from Csound itself are combined in the 
	// "console" text area.
	function handleMessage(message) {
	    var messages_textarea = document.getElementById("console");
	    var existing = messages_textarea.value;
	    messages_textarea.value = existing + message.data;
	    messages_textarea.scrollTop = messages_textarea.scrollHeight;    
	};
        
    //  });
    </script>
    <script type="text/javascript" src="csound.js"></script>
    <!--The PNaCl csound module must be the very last element in the body, 
        because the "csound.js" script attaches the Csound module and its 
        callbacks to that element. -->
    <div id="engine"></div>
</body>
</html>

